cmake_minimum_required(VERSION 3.8)

if(pm_debug_included)
    return()
endif()
set(pm_debug_included 1)

function(pm_print_prop target prop)
    get_property(value TARGET "${target}" PROPERTY "${prop}")
    if(NOT "${value}" STREQUAL "")
        message(STATUS "${prop}=${value}")
    endif()
endfunction()

function(pm_print_prop_cfg target prop cfg)
    get_property(value TARGET "${target}" PROPERTY "${prop}_${cfg}")
    if(NOT "${value}" STREQUAL "")
        message(STATUS "${prop}_${cfg}=${value}")
    endif()
endfunction()

function(pm_print_imported_props target)
    get_property(imported_configs TARGET "${target}" PROPERTY IMPORTED_CONFIGURATIONS)
    foreach(cfg ${imported_configs})
        pm_print_prop_cfg("${target}" IMPORTED_LOCATION "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_IMPLIB "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_LINK_DEPENDENT_LIBRARIES "${cfg}")
        #pm_print_prop_cfg("${target}" IMPORTED_LINK_INTERFACE_LANGUAGES "${cfg}") commented because always CXX in our case
        pm_print_prop_cfg("${target}" IMPORTED_LINK_INTERFACE_LIBRARIES "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_LINK_INTERFACE_MULTIPLICITY "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_NO_SONAME "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_OBJECTS "${cfg}")
        pm_print_prop_cfg("${target}" IMPORTED_SONAME "${cfg}")
    endforeach()
endfunction()

function(pm_print_interface_props target)
    pm_print_prop("${target}" INTERFACE_AUTOUIC_OPTIONS)
    pm_print_prop("${target}" INTERFACE_COMPILE_DEFINITIONS)
    pm_print_prop("${target}" INTERFACE_COMPILE_FEATURES)
    pm_print_prop("${target}" INTERFACE_COMPILE_OPTIONS)
    pm_print_prop("${target}" INTERFACE_INCLUDE_DIRECTORIES)
    pm_print_prop("${target}" INTERFACE_LINK_LIBRARIES)
    pm_print_prop("${target}" INTERFACE_POSITION_INDEPENDENT_CODE)
    pm_print_prop("${target}" INTERFACE_SOURCES)
    pm_print_prop("${target}" INTERFACE_SYSTEM_INCLUDE_DIRECTORIES)
endfunction()

function(pm_print_props target)
    message(STATUS "------------------- ${target} properties -------------------")
    pm_print_imported_props("${target}")
    pm_print_interface_props("${target}")
    message(STATUS "")
endfunction()
